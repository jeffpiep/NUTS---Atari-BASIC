9 R=PEEK(106):POKE106,R-8:GR.1:POKE106,R:CLS
20 SE.0,1,2
35 REM MODIFY DISPLAY LIST
40 FOR I=7 TO 18: READ A: POKE DPEEK(560)+I,A: N.I
41 DATA 39,39,39,39,39,39,39,39,39,39,7,65

50 DIM S$(16+24+16),T$(8)
60 FOR I=1 TO 56: READ A: S$(I)=CHR$(A) : N.I
69 REM SQUIRREL LEFT RIGHT
70 DATA 103,181,253,60,124,60,56,240,230,173,191,60,62,60,28,15
80 REM BLUEJAY LEFT RIGHT
90 DATA 0,0,120,208,112,96,240,120,136,4,0,0,0,0,30,11,14,6,15,30,17,32,0,0
91 REM ACORN
92 DATA 0,0,0,0,24,16,124,124,56,56,56,16,0,0,0,0
93 FOR I=1 TO 8: READ A: T$(I)=CHR$(A) : N.I: REM TREEBARK
95 DATA 74,73,73,73,169,173,182,178
97 M.224*256,(R-8)*256,1024:M.ADR(T$),(R-8)*256+3*8,8:POKE 756,R-8:GOSUB 1210
98 ?#6;"nuts!":?#6;"time:"," score:":FOR X=0 TO 10:?#6;" ######      ######":N.X
100 L=1:F=1:S=16:H=104 :T=1001:BB=1:B1=12:CC=0:C1=16:SS=0:SC=0
105 REPEAT:POKE HITCLR,1
110 T=T-2:A=2*STRIG(0)
111 IF F=0:F=PTRIG(0)-PTRIG(1):END.
120 IF A=0
121 T=T+1:S=S-4:IF S<0:S=15:ENDIF:-M.PM+640+24,PM+640+26,78:-M.PM+640+128+24,PM+640+128+26,78
122 IF BB:B1=B1-2:M.ADR(S$)+16+6-BB*6+B1,PM+640+24+64-64*BB,2:BB=BB*(B1>0):ELSE:B1=12:BB=(RND>.98)-(RND<.02):END.
125 END. 
130 IF CC:C1=C1-4+A:M.ADR(S$)+40+C1,PM+640+256+24,4-A:CC=C1>0:ELSE:C1=16:CC=(2-A)*(RND>.90):ENDIF
131 -M.PM+640+256+24,PM+640+256+28-A,78
135 IF F=-1:IF H=104:F=0:EL.:H=H-8:M.ADR(S$),PM+513+70,8:END.:END.
136 IF F=1: IF H=136:F=0:ELSE:H=H+8:M.ADR(S$)+8,PM+513+70,8:END.:END.: POKE HPOSP0,H
140 P=PEEK(P0PL):IF P&8:SC=SC+1:-M.PM,PM+640+256+60,30:POS.17,1:?#6;SC; :ELSE: IF P&6 :L=L-1:END.:END.
150 POS.5,1:?#6;T;"  ";
180 REPEAT: UNTIL PEEK(54283)>95: POKE 54277,S
200 UNTIL (T<1)!(L=0) :POS.8,6:?#6;"game";:POS.8,7:?#6;"over":IF SC>G:G=SC:END.:END

999 REM INITIALIZE PM
1210 PMBASE=54279:REM                       Player-missile base pointer
1220 GRACTL=53277:REM                       CTIA graphics control register
1230 HPOSP0=53248:REM                       Horizontal position of P0
1231 P0PL=53260:REM PLAYER 0 TO PLAYER COLLISION REGISTER
1232 HITCLR=53278: REM POKE 1 TO CLEAR COLLISION REGISTERS
1240 PMW=53256: REM WIDTH REGISTER
1270 POKE PMBASE,R-4:REM              Tell ANTIC where PM RAM is
1280 PM=(R-4)*256:REM                     Keep track of PM RAM address
1290 POKE 559,46:REM                     Enable PM DMA with 2-line res
1291 DP.PMW,257: DP.PMW+2,257:REM DOUBLE WIDTH
1300 POKE GRACTL,3:REM                      Enable PM display
1310 REM: POKE HPOSP0,135:REM                    Declare horizontal position
1311 POKE HPOSP0+1,104:REM                    Declare horizontal position
1312 POKE HPOSP0+2,136:REM                    Declare horizontal position
1313 POKE HPOSP0+3,120:REM                    Declare horizontal position
2100 REM POKE PM,0:M.PM,PM+1,1023
2180 DP.704,$850A:DP.706,$1D85
2200 POKE HITCLR,1:RETURN
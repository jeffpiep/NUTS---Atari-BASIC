9 RAMTOP = PEEK(106): POKE 106,RAMTOP-8
20 GRAPHICS 1:GOSUB 1210
30 DL=DPEEK(560): M=DPEEK(DL+4)
35 REM MODIFY DISPLAY LIST
40 FOR I=0 TO 22: READ A: POKE DL+I,A: N.I
41 DATA 112,112,112,70,128,189,6,103,168,189,39,39,39,39,39,39,39,39,39,39,65,128,189
42 DPOKE DL+4,M: DPOKE DL+8,M+40
43 PRINT #6;"   SCROLLING GAME"
44 PRINT #6;"score:","lives:"
50 MEMST=DPEEK(DL+8): DPOKE DL+8,MEMST+220
59 REM FILL THE VIDEO BUFFER WITH WALLS
60 FOR X=0 TO 220 step 20
65 A=33+X*0.05:B=33+5-X*0.05
70 POKE MEMST+5+X,A:POKE MEMST+14+X,A
71 POKE MEMST+5+X+240,A:POKE MEMST+14+X+240,A
80 N.X
99 REM SCROLL THE BUFFER
100 F=1: X=220: S=15
105 REPEAT
110 IF STRIG(0)=0:S = S-1: IF S=-1:S=15:X=X-20: ENDIF : IF X=-20:X=220: ENDIF : ENDIF
120 POKE 54277,S: FOR Y=1TO1:N.Y: DPOKE DL+8,MEMST+X
200 UNTIL F=0 

999 REM INITIALIZE PM
1210 PMBASE=54279:REM                       Player-missile base pointer
1220 GRACTL=53277:REM                       CTIA graphics control register
1230 HPOSP0=53248:REM                       Horizontal position of P0
1240 PMW=53256: REM WIDTH REGISTER
1270 POKE PMBASE,RAMTOP-4:REM              Tell ANTIC where PM RAM is
1280 PM=(RAMTOP-4)*256:REM                     Keep track of PM RAM address
1290 POKE 559,46:REM                     Enable PM DMA with 2-line res
1291 POKE PMW,1: REM DOUBLE WIDTH
1300 POKE GRACTL,3:REM                      Enable PM display
1310 POKE HPOSP0,150:REM                    Declare horizontal position
2100 FOR I=PM+513 TO PM+640: REM store player in unused RAM
2110 POKE I,0: NEXT I
2120 FOR I=PM+513+60 TO PM+513+60+11: POKE I,255: NEXT I
2180 POKE 704,88:REM                     Make the player pink
2200 RETURN